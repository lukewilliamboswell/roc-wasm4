app "rocci-bird"
    packages {
        w4: "../platform/main.roc",
    }
    imports [
        w4.Task.{ Task },
        w4.W4.{ Gamepad },
        w4.Sprite.{ Sprite },
    ]
    provides [main, Model] to w4

Program : {
    init : Task Model [],
    update : Model -> Task Model [],
}

Model : [
    TitleScreen TitleScreenState,
    Game GameState,
    GameOver GameOverState,
]

main : Program
main = { init, update }

init : Task Model []
init =
    # Lospec palette: Candy Cloud [2-BIT] Palette
    palette = {
        color1: 0xe6e6c0,
        color2: 0xb494b7,
        color3: 0x42436e,
        color4: 0x26013f,
    }

    {} <- W4.setPalette palette |> Task.await

    frameCount <- loadFrameCount |> Task.await
    Task.ok (initTitleScreen frameCount)

update : Model -> Task Model []
update = \model ->
    when model is
        TitleScreen prev ->
            state <- updateFrameCount prev |> Task.await
            runTitleScreen state

        Game prev ->
            state <- updateFrameCount prev |> Task.await
            runGame state

        GameOver prev ->
            state <- updateFrameCount prev |> Task.await
            runGameOver state

updateFrameCount : { frameCount : U64 }a -> Task { frameCount : U64 }a []
updateFrameCount = \prev ->
    frameCount = Num.addWrap prev.frameCount 1
    {} <- saveFrameCount frameCount |> Task.await
    Task.ok { prev & frameCount }

# ===== Title Screen ======================================

TitleScreenState : {
    frameCount : U64,
    pipe : Pipe,
    rocciIdleAnim : Animation,
    groundSprite : Sprite,
    pipeSprite : Sprite,
}

initTitleScreen : U64 -> Model
initTitleScreen = \frameCount ->
    TitleScreen {
        frameCount,
        pipe: { x: 140, gapStart: 50 },
        rocciIdleAnim: createRocciIdleAnim frameCount,
        groundSprite: createGroundSprite {},
        pipeSprite: createPipeSprite {},
    }

runTitleScreen : TitleScreenState -> Task Model []
runTitleScreen = \prev ->
    state = { prev &
        rocciIdleAnim: updateAnimation prev.frameCount prev.rocciIdleAnim,
    }

    {} <- setTextColors |> Task.await
    {} <- W4.text "Rocci Bird!!!" { x: 32, y: 12 } |> Task.await
    {} <- W4.text "Click to start!" { x: 24, y: 72 } |> Task.await

    {} <- drawPipe state.pipeSprite state.pipe |> Task.await
    {} <- drawGround state.groundSprite |> Task.await

    shift =
        (state.frameCount // halfRocciIdleAnimTime + 1) % 2 |> Num.toI32

    {} <- drawAnimation state.rocciIdleAnim { x: 70, y: 40 + shift } |> Task.await
    gamepad <- W4.getGamepad Player1 |> Task.await
    mouse <- W4.getMouse |> Task.await

    start = gamepad.button1 || gamepad.up || mouse.left

    if start then
        # Seed the randomness with number of frames since the start of the game.
        # This makes the game feel like it is truely randomly seeded cause players won't always start on the same frame.
        {} <- W4.seedRand state.frameCount |> Task.await

        Task.ok (initGame state)
    else
        Task.ok (TitleScreen state)

# ===== Main Game =========================================

GameState : {
    frameCount : U64,
    score : U8,
    player : {
        y : F32,
        yVel : F32,
    },
    lastPipeGenerated : U64,
    pipes : List Pipe,
    lastFlap : Bool,
    rocciFlapAnim : Animation,
    pipeSprite : Sprite,
    groundSprite : Sprite,
}

initGame : TitleScreenState -> Model
initGame = \{ frameCount, pipeSprite, groundSprite, pipe } ->
    Game {
        frameCount,
        score: 0,
        player: {
            y: 60,
            yVel: 0.5,
        },
        lastPipeGenerated: frameCount,
        pipes: [pipe],
        lastFlap: Bool.true,
        rocciFlapAnim: createRocciFlapAnim frameCount,
        pipeSprite,
        groundSprite,
    }

# With out explicit typing `f32`, roc fails to compile this.
# TODO: finetune gravity and jump speed
gravity = 0.15f32
jumpSpeed = -3.0f32

runGame : GameState -> Task Model []
runGame = \prev ->
    gamepad <- W4.getGamepad Player1 |> Task.await
    mouse <- W4.getMouse |> Task.await

    flap = gamepad.button1 || gamepad.up || mouse.left
    flapAllowed = prev.player.y > 20 && prev.rocciFlapAnim.state == Completed

    (yVel, nextAnim, soundTask) =
        if !prev.lastFlap && flap && flapAllowed then
            anim = prev.rocciFlapAnim
            (
                jumpSpeed,
                { anim & index: 0, state: RunOnce },
                W4.tone {
                    startFreq: 700,
                    endFreq: 870,
                    channel: Pulse1 Quarter,
                    attackTime: 10,
                    sustainTime: 0,
                    decayTime: 3,
                    releaseTime: 5,
                    volume: 10,
                    peakVolume: 20,
                },
            )
        else
            (
                prev.player.yVel + gravity,
                updateAnimation prev.frameCount prev.rocciFlapAnim,
                Task.ok {},
            )

    {} <- soundTask |> Task.await
    pipe <- maybeGeneratePipe prev.lastPipeGenerated prev.frameCount |> Task.attempt

    lastPipeGenerated =
        if Result.isOk pipe then
            prev.frameCount
        else
            prev.lastPipeGenerated

    pipes =
        prev.pipes
        |> updatePipes
        |> List.appendIfOk pipe

    gainPoint = Num.toU8 (List.countIf prev.pipes \{ x } -> x == 18)
    y = Num.max 0 (prev.player.y + yVel)
    state = { prev &
        rocciFlapAnim: nextAnim,
        player: { y, yVel },
        score: Num.addWrap prev.score gainPoint,
        lastFlap: flap,
        lastPipeGenerated,
        pipes,
    }

    pointSoundTask =
        if gainPoint > 0 then
            W4.tone {
                startFreq: 995,
                endFreq: 1000,
                channel: Pulse2 Half,
                decayTime: 10,
                releaseTime: 10,
                peakVolume: 75,
                volume: 25,
            }
        else
            Task.ok {}

    {} <- pointSoundTask |> Task.await
    {} <- drawPipes state.pipeSprite state.pipes |> Task.await
    {} <- drawGround state.groundSprite |> Task.await

    yPixel =
        Num.floor state.player.y
        |> Num.min 134

    collided <- playerCollided yPixel state.rocciFlapAnim.index |> Task.await
    {} <- drawAnimation state.rocciFlapAnim { x: playerX, y: yPixel } |> Task.await

    {} <- drawScore state.score |> Task.await

    if !collided && y < 134 then
        Task.ok (Game state)
    else
        {} <- W4.tone {
                startFreq: 170,
                endFreq: 40,
                channel: Noise,
                sustainTime: 20,
                releaseTime: 40,
            }
            |> Task.await

        Task.ok (initGameOver state)

# ===== Game Over =========================================

GameOverState : {
    frameCount : U64,
    score : U8,
    player : {
        y : F32,
        yVel : F32,
    },
    pipes : List Pipe,
    rocciFallAnim : Animation,
    pipeSprite : Sprite,
    groundSprite : Sprite,
}

initGameOver : GameState -> Model
initGameOver = \{ frameCount, score, pipeSprite, groundSprite, player, pipes } ->
    GameOver {
        frameCount,
        score,
        player,
        pipes,
        rocciFallAnim: createRocciFallAnim frameCount,
        pipeSprite,
        groundSprite,
    }

runGameOver : GameOverState -> Task Model []
runGameOver = \prev ->
    yVel = prev.player.yVel + gravity
    nextAnim = updateAnimation prev.frameCount prev.rocciFallAnim

    y =
        next = prev.player.y + yVel
        if next > 134 then
            134
        else
            next

    state = { prev &
        rocciFallAnim: nextAnim,
        player: { y, yVel },
    }

    {} <- drawPipes state.pipeSprite state.pipes |> Task.await
    {} <- drawGround state.groundSprite |> Task.await

    {} <- setTextColors |> Task.await
    {} <- W4.text "Game Over!" { x: 44, y: 56 } |> Task.await
    {} <- W4.text "Right to restart" { x: 20, y: 72 } |> Task.await
    # If this is commented out, the code will not compile.
    # Error in alias analysis
    {} <- W4.text "" { x: 0, y: 0 } |> Task.await

    {} <- drawScore state.score |> Task.await

    yPixel = Num.floor state.player.y
    {} <- drawAnimation state.rocciFallAnim { x: playerX, y: yPixel } |> Task.await

    mouse <- W4.getMouse |> Task.await
    if mouse.right then
        Task.ok (initTitleScreen state.frameCount)
    else
        Task.ok (GameOver state)

# ===== Pipes =============================================

Pipe : { x : I32, gapStart : I32 }

gapHeight = 40

drawPipes : Sprite, List Pipe -> Task {} []
drawPipes = \sprite, pipes ->
    List.walk pipes (Task.ok {}) \task, pipe ->
        {} <- task |> Task.await
        drawPipe sprite pipe

drawPipe : Sprite, Pipe -> Task {} []
drawPipe = \sprite, { x, gapStart } ->
    {} <- setSpriteColors |> Task.await
    {} <- Sprite.blit sprite { x, y: gapStart - W4.screenHeight, flags: [FlipY] } |> Task.await
    Sprite.blit sprite { x, y: gapStart + gapHeight }

updatePipes : List Pipe -> List Pipe
updatePipes = \pipes ->
    pipes
    |> List.map \pipe -> { pipe & x: pipe.x - 1 }
    |> List.dropIf \pipe -> pipe.x < -20

maybeGeneratePipe : U64, U64 -> Task Pipe [NoPipe]
maybeGeneratePipe = \lastgenerated, framecount ->
    if framecount - lastgenerated > 90 then
        gapStart <- W4.randBetween { start: 0, before: 16 } |> Task.await
        Task.ok { x: W4.screenWidth, gapStart: gapStart * 5 + 10 }
    else
        Task.err NoPipe

# ===== Animations ========================================

AnimationState : [Completed, RunOnce, Loop]
Animation : {
    lastUpdated : U64,
    index : U64,
    cells : List { frames : U64, sprite : Sprite },
    state : AnimationState,
}

updateAnimation : U64, Animation -> Animation
updateAnimation = \frameCount, anim ->
    framesPerUpdate =
        when List.get anim.cells (Num.toNat anim.index) is
            Ok { frames } ->
                frames

            Err _ ->
                crash "animation cell out of bounds at index: \(anim.index |> Num.toStr)"

    if frameCount - anim.lastUpdated < framesPerUpdate then
        anim
    else
        nextIndex = wrappedInc anim.index (List.len anim.cells |> Num.toU64)
        when anim.state is
            Completed ->
                { anim & lastUpdated: frameCount }

            Loop ->
                { anim & index: nextIndex, lastUpdated: frameCount }

            RunOnce ->
                if nextIndex == 0 then
                    { anim & state: Completed, lastUpdated: frameCount }
                else
                    { anim & index: nextIndex, lastUpdated: frameCount }

drawAnimation : Animation, { x : I32, y : I32, flags ? List [FlipX, FlipY, Rotate] } -> Task {} []
drawAnimation = \anim, { x, y, flags ? [] } ->
    when List.get anim.cells (Num.toNat anim.index) is
        Ok { sprite } ->
            {} <- setSpriteColors |> Task.await
            Sprite.blit sprite { x, y, flags }

        Err _ ->
            crash "animation cell out of bounds at index: \(anim.index |> Num.toStr)"

wrappedInc = \val, count ->
    next = val + 1
    if next == count then
        0
    else
        next

# ===== Misc, Drawing, and Color ==========================

playerX = 20

playerCollided : I32, U64 -> Task Bool []
playerCollided = \playerY, animIndex ->
    # This is written in a kinda silly but simple way.
    # It checks to ensure a few points in the sprite are all background colored.
    # This must be run before drawing the player.
    basePoints = [
        { x: 11, y: 2 },
        { x: 13, y: 3 },
        { x: 3, y: 5 },
        { x: 11, y: 6 },
        { x: 9, y: 8 },
        { x: 5, y: 9 },
        { x: 7, y: 10 },
        { x: 5, y: 12 },
    ]

    collisionPoints =
        if animIndex == 2 then
            basePoints
            |> List.append { x: 2, y: 1 }
            |> List.append { x: 7, y: 1 }
        else if animIndex == 1 then
            basePoints
            |> List.append { x: 2, y: 2 }
        else
            basePoints

    List.walk collisionPoints (Task.ok Bool.false) \collidedTask, { x, y } ->
        collided <- collidedTask |> Task.await
        if collided then
            Task.ok Bool.true
        else
            point = {
                x: Num.toU8 (playerX + x),
                y: Num.toU8 (playerY + y),
            }
            color <- W4.getPixel point |> Task.await
            Task.ok (color != Color1)

# Since wasm4 doesn't have a proper source of randomness,
# save and restore the frame count as the seed.
# This was a random recommendation online and seems reasonable.
loadFrameCount : Task U64 []
loadFrameCount =
    data <- W4.loadFromDisk |> Task.await

    when Num.bytesToU64 data 0 is
        Ok count ->
            Task.ok count

        Err OutOfBounds ->
            Task.ok 0

saveFrameCount : U64 -> Task {} []
saveFrameCount = \frameCount ->
    b0 = frameCount |> Num.bitwiseAnd 0xFF |> Num.toU8
    b1 = frameCount |> Num.shiftRightZfBy 8 |> Num.bitwiseAnd 0xFF |> Num.toU8
    b2 = frameCount |> Num.shiftRightZfBy 16 |> Num.bitwiseAnd 0xFF |> Num.toU8
    b3 = frameCount |> Num.shiftRightZfBy 24 |> Num.bitwiseAnd 0xFF |> Num.toU8
    b4 = frameCount |> Num.shiftRightZfBy 32 |> Num.bitwiseAnd 0xFF |> Num.toU8
    b5 = frameCount |> Num.shiftRightZfBy 40 |> Num.bitwiseAnd 0xFF |> Num.toU8
    b6 = frameCount |> Num.shiftRightZfBy 48 |> Num.bitwiseAnd 0xFF |> Num.toU8
    b7 = frameCount |> Num.shiftRightZfBy 56 |> Num.bitwiseAnd 0xFF |> Num.toU8

    W4.saveToDisk [b0, b1, b2, b3, b4, b5, b6, b7]
    |> Task.onErr \_ -> Task.ok {}

drawScore : U8 -> Task {} []
drawScore = \score ->
    {} <- setTextColors |> Task.await
    x =
        if score < 10 then
            76
        else if score < 100 then
            72
        else
            68
    W4.text "$(Num.toStr score)" { x, y: 4 }

drawGround : Sprite -> Task {} []
drawGround = \sprite ->
    {} <- setGroundColors |> Task.await
    Sprite.blit sprite { x: 0, y: W4.screenHeight - 12 }

setTextColors : Task {} []
setTextColors =
    W4.setTextColors { fg: Color4, bg: None }

setSpriteColors : Task {} []
setSpriteColors =
    W4.setDrawColors { primary: None, secondary: Color2, tertiary: Color3, quaternary: Color4 }

setGroundColors : Task {} []
setGroundColors =
    W4.setDrawColors { primary: Color1, secondary: Color2, tertiary: Color3, quaternary: Color4 }

halfRocciIdleAnimTime = 20

rocciSpriteSheet = Sprite.new {
    data: [0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x15, 0x56, 0x00, 0x00, 0x10, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x81, 0x00, 0x00, 0x00, 0x80, 0x40, 0x05, 0x56, 0x81, 0x80, 0x14, 0x00, 0x01, 0x80, 0x00, 0x00, 0x01, 0x80, 0x00, 0x02, 0x85, 0x00, 0x00, 0x02, 0x81, 0x40, 0x01, 0x56, 0xa5, 0xa0, 0x15, 0x00, 0x05, 0xa0, 0x00, 0x00, 0x05, 0xa0, 0x00, 0x0a, 0x95, 0x00, 0x00, 0x0a, 0x85, 0x40, 0x00, 0x56, 0xa9, 0x00, 0x15, 0x56, 0xa5, 0x00, 0x00, 0x00, 0x15, 0x00, 0x00, 0x06, 0x55, 0x00, 0x00, 0x06, 0x55, 0x40, 0x00, 0x06, 0xaa, 0x00, 0x15, 0x5a, 0xaa, 0x00, 0x15, 0x56, 0xaa, 0x00, 0x00, 0x16, 0x95, 0x00, 0x00, 0x06, 0x95, 0x00, 0x00, 0x09, 0x55, 0x00, 0x15, 0x6a, 0xa9, 0x00, 0x05, 0x56, 0xa9, 0x00, 0x00, 0x16, 0x94, 0x00, 0x00, 0x16, 0x95, 0x00, 0x00, 0x09, 0x54, 0x00, 0x05, 0x6a, 0x94, 0x00, 0x01, 0x56, 0xa4, 0x00, 0x00, 0x1a, 0x94, 0x00, 0x00, 0x16, 0x94, 0x00, 0x00, 0x09, 0x50, 0x00, 0x00, 0x69, 0x50, 0x00, 0x00, 0x56, 0x90, 0x00, 0x00, 0x1a, 0xa0, 0x00, 0x00, 0x1a, 0xa0, 0x00, 0x00, 0x29, 0x40, 0x00, 0x00, 0x29, 0x40, 0x00, 0x00, 0x26, 0x40, 0x00, 0x00, 0x0a, 0xa0, 0x00, 0x00, 0x0a, 0xa0, 0x00, 0x00, 0x29, 0x00, 0x00, 0x00, 0x29, 0x00, 0x00, 0x00, 0x29, 0x00, 0x00, 0x00, 0x0a, 0x80, 0x00, 0x00, 0x0a, 0x80, 0x00, 0x00, 0x2a, 0x00, 0x00, 0x00, 0x2a, 0x00, 0x00, 0x00, 0x2a, 0x00, 0x00, 0x00, 0x05, 0x40, 0x00, 0x00, 0x05, 0x40, 0x00, 0x00, 0x28, 0x00, 0x00, 0x00, 0x28, 0x00, 0x00, 0x00, 0x28, 0x00, 0x00, 0x00, 0xa5, 0x00, 0x00, 0x00, 0xa5, 0x00, 0x00, 0x00, 0x20, 0x00, 0x00, 0x00, 0x20, 0x00, 0x00, 0x00, 0x20, 0x00, 0x00, 0x00, 0x24, 0x00, 0x00, 0x00, 0x24, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00],
    bpp: BPP2,
    width: 80,
    height: 16,
}

createRocciIdleAnim : U64 -> Animation
createRocciIdleAnim = \frameCount -> {
    lastUpdated: frameCount,
    index: 0,
    state: Loop,
    cells: [
        { frames: 17, sprite: Sprite.subOrCrash rocciSpriteSheet { srcX: 0, srcY: 0, width: 16, height: 16 } },
        { frames: 6, sprite: Sprite.subOrCrash rocciSpriteSheet { srcX: 16, srcY: 0, width: 16, height: 16 } },
        { frames: 17, sprite: Sprite.subOrCrash rocciSpriteSheet { srcX: 32, srcY: 0, width: 16, height: 16 } },
    ],
}

createRocciFlapAnim : U64 -> Animation
createRocciFlapAnim = \frameCount -> {
    lastUpdated: frameCount,
    index: 2,
    state: Completed,
    cells: [
        { frames: 6, sprite: Sprite.subOrCrash rocciSpriteSheet { srcX: 16, srcY: 0, width: 16, height: 16 } },
        { frames: 12, sprite: Sprite.subOrCrash rocciSpriteSheet { srcX: 32, srcY: 0, width: 16, height: 16 } },
        { frames: 1, sprite: Sprite.subOrCrash rocciSpriteSheet { srcX: 0, srcY: 0, width: 16, height: 16 } },
    ],
}

createRocciFallAnim : U64 -> Animation
createRocciFallAnim = \frameCount -> {
    lastUpdated: frameCount,
    index: 0,
    state: Loop,
    cells: [
        { frames: 10, sprite: Sprite.subOrCrash rocciSpriteSheet { srcX: 48, srcY: 0, width: 16, height: 16 } },
        { frames: 10, sprite: Sprite.subOrCrash rocciSpriteSheet { srcX: 64, srcY: 0, width: 16, height: 16 } },
    ],
}

createGroundSprite : {} -> Sprite
createGroundSprite = \{} ->
    Sprite.new {
        data: [0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x44, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x11, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x65, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x59, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x95, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x55, 0x45, 0x14, 0x51, 0x85, 0x14, 0x51, 0x49, 0x14, 0x91, 0x45, 0x18, 0x51, 0x54, 0x51, 0x49, 0x24, 0x51, 0x46, 0x14, 0x51, 0x46, 0x15, 0x14, 0x61, 0x45, 0x14, 0x92, 0x45, 0x14, 0x61, 0x46, 0x14, 0x55, 0x14, 0x51, 0x45, 0x14, 0x61, 0x51, 0x45, 0x65, 0x66, 0x56, 0x56, 0x59, 0x56, 0x59, 0x56, 0x55, 0x65, 0x65, 0x56, 0x59, 0x56, 0x59, 0x95, 0x59, 0x59, 0x55, 0x99, 0x59, 0x59, 0x55, 0x95, 0x96, 0x55, 0x99, 0x55, 0x95, 0x95, 0x59, 0x55, 0x96, 0x55, 0x59, 0x59, 0x95, 0x95, 0x96, 0x55, 0x95, 0x95, 0x99, 0x66, 0x65, 0x99, 0x65, 0x96, 0x95, 0x96, 0x59, 0x59, 0x65, 0x99, 0x65, 0xa5, 0x65, 0x96, 0x56, 0x56, 0x65, 0x99, 0x96, 0x59, 0x99, 0x66, 0x5a, 0x56, 0x59, 0x65, 0x96, 0x56, 0x59, 0x66, 0x65, 0x65, 0x66, 0x59, 0x99, 0x59, 0xa6, 0x9a, 0x5a, 0x69, 0x6a, 0x5a, 0x66, 0xa5, 0xa6, 0x9a, 0x9a, 0x6a, 0x6a, 0x5a, 0x65, 0x69, 0xa6, 0xa6, 0x9a, 0x69, 0x69, 0xa5, 0xa6, 0x9a, 0x5a, 0x96, 0x56, 0x9a, 0x6a, 0x6a, 0xa6, 0x9a, 0x9a, 0x96, 0xa9, 0xa6, 0x96, 0x9a, 0x5a, 0x69, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa, 0xaa],
        bpp: BPP2,
        width: 160,
        height: 12,
    }

createPipeSprite : {} -> Sprite
createPipeSprite = \{} ->
    Sprite.new {
        data: [0x0a, 0xaa, 0xaa, 0xab, 0xf0, 0x25, 0x55, 0x55, 0x55, 0x5c, 0x26, 0x96, 0x6a, 0x9a, 0xac, 0x36, 0x96, 0x6a, 0x66, 0xac, 0x36, 0x96, 0x6a, 0x9a, 0xac, 0x0f, 0xff, 0xff, 0xff, 0xf0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0, 0x03, 0x65, 0x9a, 0x66, 0xc0, 0x03, 0x65, 0x9a, 0x9a, 0xc0],
        bpp: BPP2,
        width: 20,
        height: 160,
    }
